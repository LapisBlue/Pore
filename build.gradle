// Apply Gradle plugins
plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'checkstyle'

    id 'net.minecrell.gitpatcher' version '0.8'
    id 'blue.lapis.methodremapper' version '0.1.3'

    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

defaultTasks 'clean', 'licenseFormat', 'build'

// Project information
version = '1.0.0-SNAPSHOT'

apply from: 'gradle/deploy.gradle'

allprojects {
    group = 'blue.lapis.pore'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }
}

// Project repositories
repositories {
    jcenter()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'lapis'
        url = 'http://repo.lapis.blue/releases'
    }
}

patches {
    submodule = 'lib/Bukkit'
    target = file('Porekit')
    patches = file('patches')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
}

project('Porekit') {
    apply from: rootProject.file('bukkit.gradle')
    compileJava.dependsOn rootProject.tasks.applyPatches
}

configurations {
    shadow
    compile {
        extendsFrom shadow
    }
    shadowRuntime {
        extendsFrom shadow
    }
    runtime {
        extendsFrom shadowRuntime
    }
}

// Project dependencies
dependencies {
    compile project(':ap')

    // To update, go to https://repo.spongepowered.org/maven/org/spongepowered/spongeapi/2.1-SNAPSHOT/
    // Then pick latest version (or whatever version you want)
    compile 'org.spongepowered:spongeapi:3.0.0'
    runtime 'blue.lapis.methodremapper:tweaker:0.1'

    shadow project('Porekit')
    shadowRuntime 'mysql:mysql-connector-java:5.1.38'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testRuntime 'org.slf4j:slf4j-simple:1.7.13'
}

allprojects {
    // Read source files using UTF-8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '-Xlint:all',
            '-Xlint:-deprecation'
    ]
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events 'skipped'
    }
}

compileTestJava {
    options.compilerArgs += '-proc:none'
}

// Remap the older Bukkit methods
remap {
    inputTask = shadowJar
}

jar {
    manifest {
        attributes(
                'Specification-Title': 'Bukkit',
                'Specification-Version': project('Porekit').version,
                'Specification-Vendor': 'SpigotMC',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version + versionSuffix,
                'Implementation-Vendor': 'Lapis',
                'Main-Class': 'blue.lapis.pore.util.InfoMain'
        )
    }
}

shadowJar {
    classifier = ''
    exclude 'remap.txt'

    configurations = [project.configurations.shadowRuntime]
    dependencies {
        exclude dependency('com.google.guava:guava')
        exclude dependency('com.google.code.gson:gson')
        exclude dependency('org.yaml:snakeyaml')
    }
}

configure([project, project(':ap')]) {
    apply plugin: 'license'

    // License header formatting
    license {
        header rootProject.file('LICENSE')

        include '**/*.java'
        exclude '**/lib/**'

        sourceSets = project.sourceSets
        ignoreFailures false
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }
}

checkstyle {
    toolVersion = '6.14.1'
    configProperties = [
            'basedir': project.projectDir,
            'severity': 'warning'
    ]
    configFile = file('etc/checkstyle.xml')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
